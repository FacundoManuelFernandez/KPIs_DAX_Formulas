//Esta fórmula mide la cantidad promedio de cuotas correspondiente a los pagos concretados.

Promedio de Cuotas Concretadas =
VAR PagosConCuotas = //Crea una tabla virtual que agrega columnas auxiliares al conjunto de pagos
    ADDCOLUMNS(
        'TablaPagos',
        "CuotaReal",
            LOOKUPVALUE( //Busca la cantidad real de cuotas en la tabla de dimensiones, ya que la cantidad de cuotas en TablaPagos no siempre representa el valor real
                'TablaClasificacionCuotas'[Cuotas Reales], //Trae el valor de Cuotas Reales de la Tabla de Dimensiones
                'TablaClasificacionCuotas'[Cuotas S/Clasificacion Sistema], //Tomando como referencia Cuotas S/Clasificacion Sistema
                'TablaPagos'[Cantidad de cuotas] //A partir del dato de Cantidad de cuotas de TablaPagos
            ),
        "PagosRealizados", //Agrega una columna que contabiliza pagos realizados
            CALCULATE(
                COUNTROWS('TablaPagos'),
                'TablaPagos'[Estado de pago] = "Realizado"
            ),
        "PagosAnulados", //Agrega una columna que contabiliza pagos anulados
            CALCULATE(
                COUNTROWS('TablaPagos'),
                'TablaPagos'[Estado de pago] = "Anulado"
            )
    )
VAR CuotasPonderadas = //Calcula la suma ponderada de las cuotas reales, ponderadas por los pagos concretados
    SUMX(
        PagosConCuotas,
        VAR CuotaReal = [CuotaReal] //Traemos el dato de las Cuotas Reales
        VAR PagosConcretados = [PagosRealizados] - [PagosAnulados] //Creamos una variable que calcula los pagos concretados
        RETURN
            IF( //Evita cálculos con valores vacíos
                NOT(ISBLANK(CuotaReal)),
                CuotaReal * PagosConcretados, //Multiplica las cuotas reales por los pagos concretados
                0
            )
    )
VAR TotalPagosConcretados = //Suma total de pagos concretados (donde haya CuotaReal)
    SUMX(
        PagosConCuotas,
        VAR CuotaReal = [CuotaReal]
        VAR PagosConcretados = [PagosRealizados] - [PagosAnulados]
        RETURN
            IF(
                NOT(ISBLANK(CuotaReal)),
                PagosConcretados,
                0
            )
    )
RETURN //Devuelve el promedio ponderado: CuotasPonderadas / TotalPagosConcretados
    IF(
        TotalPagosConcretados = 0,
        BLANK(), //Evita divisiones por cero
        DIVIDE(CuotasPonderadas, TotalPagosConcretados)
    )

